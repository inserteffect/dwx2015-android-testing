import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        buildConfigField 'String', 'BUILD_TIME', '"' + new SimpleDateFormat("dd.MM.yyyy HH:mm", Locale.GERMAN).format(new Date()) + '"'
        applicationId "com.inserteffect.demo"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode project.hasProperty('versionCode') ? project.versionCode.toInteger() : 1
        versionName project.hasProperty('versionName') ? project.versionName : "SNAPSHOT"
        renderscriptTargetApi 22
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../assets/demo.keystore")
            storePassword System.getenv('TRAVIS_STORE_PASSWORD')
            keyAlias System.getenv('TRAVIS_KEY_ALIAS')
            keyPassword System.getenv('TRAVIS_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    // Add Version-Name to Android-Package.
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
}

final SUPPORT_VERSION = '22.2.0'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android support library
    compile "com.android.support:design:${SUPPORT_VERSION}"
    compile "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    compile "com.android.support:support-v4:${SUPPORT_VERSION}"
    compile "com.android.support:recyclerview-v7:${SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${SUPPORT_VERSION}"
    compile "com.android.support:support-annotations:${SUPPORT_VERSION}"

    // Util
    compile 'com.google.guava:guava:18.0'
    compile 'com.jakewharton:butterknife:6.1.0'

    // Unit testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.json:json:20140107'

    // Android unit testing
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    // Mockito
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'

    // Dagger
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    androidTestApt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}
